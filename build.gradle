plugins {
  id 'java-library'
  id 'jvm-test-suite'
  id 'maven-publish'
  id 'signing'
  id 'uk.co.developmentanddinosaurs.git-versioner' version '2.0.1'
  id 'com.diffplug.spotless' version '6.25.0'
}

group 'com.testingsyndicate'

repositories {
  mavenCentral()
}

java {
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  api 'org.junit.jupiter:junit-jupiter-api:5.10.2'
}

testing {
  suites {
    test {
      useJUnitJupiter()
      dependencies {
        implementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        implementation 'org.junit.platform:junit-platform-testkit:1.10.2'
        implementation 'org.assertj:assertj-core:3.25.3'
        implementation 'org.mockito:mockito-core:5.10.0'
        runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
      }
    }
  }
}

spotless {
  java {
    googleJavaFormat()
    endWithNewline()
  }
  groovyGradle {
    greclipse()
    indentWithSpaces(2)
    endWithNewline()
  }
}

publishing {
  repositories {
    maven {
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username = findProperty('publishUsername')
        password = findProperty('publishPassword')
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'jupiter-resources-extension'
        description = 'Easily inject file based resources into your jupiter tests'
        url = 'https://github.com/testingsyndicate/jupiter-resources-extension'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            name = 'Jack Gough'
            organization = 'Testing Syndicate'
            organizationUrl = 'https://testingsyndicate.com/'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/testingsyndicate/jupiter-resources-extension.git'
          developerConnection = 'scm:git:git://github.com/testingsyndicate/jupiter-resources-extension.git'
          url = 'https://github.com/testingsyndicate/jupiter-resources-extension.git'
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(decode(findProperty('signingKey')), '')
  sign publishing.publications.mavenJava
}

static def decode(encoded) {
  new String(Base64.getDecoder().decode(encoded ?: ''))
}
